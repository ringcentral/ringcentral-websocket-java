plugins {
  id 'java'
  id 'maven-publish'
  id 'java-library'
  id 'signing'
}

group 'com.ringcentral'
version '0.1.2'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Use JUnit test framework.
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.slf4j:slf4j-simple:1.6.2'
    implementation group: 'com.ringcentral', name: 'ringcentral', version: '2.9.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'ringcentral-websocket'
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'RingCentral WebSocket SDK'
        description = 'RingCentral WebSocket SDK'
        url = 'https://github.com/ringcentral/ringcentral-websocket-java'
        licenses {
          license {
            name = 'MIT'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id = 'tylerliu'
            name = 'Tyler Liu'
            email = 'tyler.liu@ringcentral.com'
          }
        }
        scm {
          connection = 'scm:git:git://git@github.com:ringcentral/ringcentral-websocket-java.git'
          developerConnection = 'scm:git:ssh://git@github.com:ringcentral/ringcentral-websocket-java.git'
          url = 'https://github.com/ringcentral/ringcentral-websocket-java'
        }
      }
    }
  }
  repositories {
    maven {
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

javadoc {
  options.addBooleanOption('html5', true)
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}
